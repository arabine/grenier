10 SCREEN 0:COLOR 2,0,4:KEY OFF:RANDOMIZE TIMER:CLS
11 A$(0)="O3L14CCCDCL10EF"
12 A$(1)="T200L6CCDCCDBGCFEDC"
13 A$(2)="T200O3L5FFFDFFFGFEDC"
20 INPUT "Nombre de cases horizontales :";H:IF H<2 OR H>13 THEN 20
30 INPUT "Nombre de cases verticales :";V:IF V<2 OR V>5 THEN 30
40 CLS:DIM T(H*V):P$=STRING$(3,CHR$(219)):Q$=STRING$(5,CHR$(205))
50 T$(1)=CHR$(201):T$(2)=CHR$(186):T$(3)=CHR$(204):T$(4)=CHR$(200)
60 FOR I=1 TO H
70 T$(1)=T$(1)+Q$+CHR$(203+16*(I=H))
80 T$(2)=T$(2)+STRING$(5,CHR$(32))+CHR$(186)
90 T$(3)=T$(3)+Q$+CHR$(206+21*(I=H))
100 T$(4)=T$(4)+Q$+CHR$(202+14*(I=H))
110 NEXT I
120 LOCATE 1,1:PRINT T$(1)
130 FOR J=1 TO V:PRINT T$(2):PRINT T$(2):PRINT T$(2):PRINT T$(3-(J=V)):NEXT
135 LOCATE 23,1:PRINT "ATTENDEZ SVP"
140 FOR I=1 TO H*V-1:T(I)=I:NEXT :CO=H:LO=V
150 FOR I=1 TO 300:S=INT(RND*4)+1
160 C=CO+(S=1 AND CO>1)-(S=2 AND CO<H):L=LO+(S=3 AND LO>1)-(S=4 AND LO<V)
170 SWAP T(C+(L-1)*H),T(CO+(LO-1)*H):CO=C:LO=L:NEXT
175 LOCATE 23,1:PRINT "            "
180 FOR I=1 TO H:FOR J=1 TO V:REM Affichage des nombres
190 IF T(I+(J-1)*H)=0 THEN C=I:L=J
200 LOCATE 4*J-1,6*I-3:PRINT MID$(STR$(T(I+(J-1)*H)),2);:NEXT J,I
210 COLOR 3:REM Boucle de jeu
240 LOCATE 4*L-1,6*C-3:PRINT P$;:CO=C:LO=L:S=0
250 FOR I=1 TO H*V-1:S=S+(T(I)<>I):NEXT I:IF S=0 THEN 320
260 R$=RIGHT$(INKEY$,1):IF R$="" THEN 260
270 C=C+(R$="4" AND C>1)-(R$="6" AND C<H)
280 L=L+(R$="8" AND L>1)-(R$="2" AND L<V)
285 IF R$="F" OR R$="f" THEN 310
290 IF C<>CO OR L<>LO THEN SWAP T(C+(L-1)*H),T(CO+(LO-1)*H):LOCATE 4*LO-1,6*CO-3:PRINT STR$(T(CO+(LO-1)*H));" ";:SOUND RND*1000+37,2
300 GOTO 240
310 PLAY"O3L4AO2AO1AO0L1A":END
320 PLAY A$(RND*2):END
